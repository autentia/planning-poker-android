buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public"}
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}

apply plugin: 'android'
apply plugin: 'release'



dependencies {

}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    def getVersionName = { ->
        def definedTasks = gradle.startParameter.taskNames
        def snapshotIndex = project.version.toString().indexOf("-SNAPSHOT")
        def versionName = project.version
        println("=======definedTasks:"+definedTasks)
        if(definedTasks.contains("confirmReleaseVersion") && (snapshotIndex>0)){
            versionName = project.version.toString().substring(0,snapshotIndex)
        }else {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'log', '-n 1', '--pretty=format:%H'
                standardOutput = stdout
            }
            versionName += "-"+stdout.toString().trim()
        }
        return versionName
    }

    def getVersionCode = { ->
        return Integer.valueOf(project.versionCode)+1;
    }

    defaultConfig {
        versionCode getVersionCode()
        versionName getVersionName()
        minSdkVersion 8
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        // See: http://stackoverflow.com/questions/18328730/how-to-create-a-release-signed-apk-file-using-gradle
        debug {
            storeFile file(System.getenv("HOME")+"/.android/debug.keystore")
        }
        release {
            // DEBUG version ask for values System.console, but are not used !!!!
            storeFile file(System.console().readLine("\n\$ Enter keystore path: "))
            storePassword new String(System.console().readPassword("\n\$ Enter keystore password: "))
            keyAlias System.console().readLine("\n\$ Enter key alias: ")
            keyPassword System.console().readPassword("\n\$ Enter key password: ")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlign true
        }
        debug {
            runProguard false
            zipAlign true
        }
    }
}
